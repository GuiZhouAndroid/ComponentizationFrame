plugins {
    id 'com.android.library'
}

//sonatype平台的maven中央库配置文件
//发布版
//apply from: "release-sonatype-publish.gradle"
//开发版
apply from: "snapshot-sonatype-publish.gradle"

//config.gradle根目录变量全局
def app = rootProject.ext.androidConfig
def appNamespaceConfig = rootProject.ext.namespaceConfig
def dep = rootProject.ext.dependenciesConfig

android {
    namespace appNamespaceConfig["namespace_lib_support_mvp"]
    compileSdk app["compileSdk"]
    buildToolsVersion app["buildToolsVersion"]

    defaultConfig {
        minSdk app["minSdk"]
        consumerProguardFiles "consumer-rules.pro"
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }
}

// 组件缓存更新时间设置（默认每次build都更新）
configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
}

dependencies {

    // ----------------引用远程第三方依赖库-----------------

    //androidx
    api(dep["appcompat"])
    api(dep["multidex"])
    api(dep["material"])
    //lombok
    api(dep["lombok"])

    //判断自制lib库依赖方式
    if (isUseLocalSupportLib) {
        // ----------------使用本地自制库，便于开发时修改码，不用每次都改一次都推送SonaType-----------------
        //弹窗
        api(project(':lib_support_dialog'))
        //滑动返回
        api(project(':lib_support_swipeback'))
        //工具类库
        api(project(':lib_support_utils'))
    } else {
        // ----------------引用远程自制依赖库-----------------
        //弹窗
        api(dep["my-lib-dialog"])
        //滑动返回
        api(dep["my-lib-swipeback"])
        //工具类库
        api(dep["my-lib-utils"])
    }
}